/*
 * Copyright 2002-2020. the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'org.springframework.boot' version "2.6.3"
}

description = "Spring Cloud App Broker Acceptance Tests"

dependencies {
	api enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
	api project(":spring-cloud-starter-app-broker-cloudfoundry")
	api "org.springframework.boot:spring-boot-starter-webflux"

	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "io.projectreactor:reactor-test"
	testImplementation "org.cloudfoundry:cloudfoundry-client-reactor:${cfJavaClientVersion}"
	testImplementation "org.cloudfoundry:cloudfoundry-operations:${cfJavaClientVersion}"
}

// omit the version from the test broker jar file
jar {
	archiveVersion.set ''
}
bootJar {
	archiveVersion.set ''
}

test {
	// build the test broker from /src into a jar that the tests can deploy
	dependsOn assemble

	maxParallelForks = Runtime.runtime.availableProcessors() - 1 ?: 1

	// Only run the tests if acceptanceTests is specified
	onlyIf {
		project.hasProperty("acceptanceTests")
	}
	systemProperty "tests.broker-app-path", "${buildDir}/libs/spring-cloud-app-broker-acceptance-tests.jar"
	// Pass relevant java properties to the test task
	systemProperties << System.properties.findAll { it.key.startsWith("spring.") }
}
